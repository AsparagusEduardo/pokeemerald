const BOSS_NPC = 5

mapscripts Rogue_Boss_3_MapScripts
{
}

script Rogue_Battle_Boss3
{
    lockall()
    applymovement(OBJ_EVENT_ID_PLAYER, PlayerToBattleFieldBoss3)
    waitmovement(0)

    special(SpawnCameraObject)
    applymovement(OBJ_EVENT_ID_CAMERA, CameraToBattleFieldBoss3)
    waitmovement(0)

    msgbox(format("FLANNERY: A new challenger!"))
    
    setweather(WEATHER_SUNNY_CLOUDS)
    doweather

    applymovement(BOSS_NPC, BossToFightBoss3)
    applymovement(OBJ_EVENT_ID_PLAYER, PlayerToFightBoss3)
    waitmovement(0)

    trainerbattle_single(TRAINER_FLANNERY_1, format("Don't hold back now!"), format("Well fought trainer!"), Rogue_BattleDefeated_Boss3)
}

script Rogue_BattleDefeated_Boss3
{
    message(format("{PLAYER} received the ?? BADGE!"))
    waitmessage()
    call(Common_EventScript_PlayGymBadgeFanfare)
    setflag(FLAG_BADGE02_GET)
    setflag(FLAG_ROGUE_DEFEATED_FLANNERY)
    msgbox(format("Good luck in your future battles!"))
    waitmessage()

    special(RemoveCameraObject)
    warp(MAP_ROGUE_HUB_TRANSITION, 0)
}

movement PlayerToBattleFieldBoss3 {
    walk_up * 10
    walk_right
}

movement CameraToBattleFieldBoss3 {
    walk_right * 4
}

movement PlayerToFightBoss3 {
    walk_right
    face_right
}

movement BossToFightBoss3 {
    walk_left
}