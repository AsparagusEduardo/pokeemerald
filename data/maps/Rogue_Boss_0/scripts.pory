const BOSS_NPC = 5

mapscripts Rogue_Boss_0_MapScripts
{
}

script Rogue_Battle_Boss0
{
    lockall()
    applymovement(OBJ_EVENT_ID_PLAYER, PlayerToBattleField)
    waitmovement(0)

    special(SpawnCameraObject)
    applymovement(OBJ_EVENT_ID_CAMERA, CameraToBattleField)
    waitmovement(0)

    msgbox(format("ROXANNE: A new challenger!"))

    setweather(WEATHER_SANDSTORM)
    doweather

    applymovement(BOSS_NPC, BossToFight)
    applymovement(OBJ_EVENT_ID_PLAYER, PlayerToFight)
    waitmovement(0)
    
    // Manually kick off the encouter to control the music + avoid the trainer from changing directions
    playbgm(MUS_ENCOUNTER_INTENSE, FALSE)
    msgbox(format("Don't hold back now!"))
    trainerbattle(TRAINER_BATTLE_SINGLE_NO_INTRO_TEXT, TRAINER_ROXANNE_1, 0, format("Well fought trainer!"))
    call(Rogue_BattleDefeated_Boss0)
}

script Rogue_BattleDefeated_Boss0
{
    message(format("{PLAYER} received a GYM BADGE!"))
    waitmessage()
    call(Common_EventScript_PlayGymBadgeFanfare)
    setflag(FLAG_BADGE01_GET)
    setflag(FLAG_ROGUE_DEFEATED_BOSS00)
    msgbox(format("Good luck in your future battles!"))
    waitmessage()
    special(HealPlayerParty)
    special(RemoveCameraObject)
    warp(MAP_ROGUE_HUB_TRANSITION, 0)
}

movement PlayerToBattleField {
    walk_up * 10
    walk_right
}

movement CameraToBattleField {
    walk_right * 4
}

movement PlayerToFight {
    walk_right
    face_right
}

movement BossToFight {
    walk_left
}