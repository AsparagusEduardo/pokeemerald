const BOSS_NPC = 1

mapscripts Rogue_Boss_11_MapScripts
{
}

script Rogue_Battle_Boss11
{
    lockall()
    applymovement(OBJ_EVENT_ID_PLAYER, PlayerToBattleFieldBoss11)
    waitmovement(0)

    special(SpawnCameraObject)
    applymovement(OBJ_EVENT_ID_CAMERA, CameraToBattleFieldBoss11)
    waitmovement(0)

    msgbox(format("DRAKE: A new challenger!"))

    setweather(WEATHER_SUNNY)
    doweather

    applymovement(BOSS_NPC, BossToFightBoss11)
    applymovement(OBJ_EVENT_ID_PLAYER, PlayerToFightBoss11)
    waitmovement(0)
    
    // Manually kick off the encouter to control the music + avoid the trainer from changing directions
    playbgm(MUS_ENCOUNTER_ELITE_FOUR, FALSE)
    msgbox(format("Don't hold back now!"))
    trainerbattle(TRAINER_BATTLE_SINGLE_NO_INTRO_TEXT, TRAINER_DRAKE, 0, format("Well fought trainer!"))
    call(Rogue_BattleDefeated_Boss11)
}

script Rogue_BattleDefeated_Boss11
{
    message(format("{PLAYER} received a ELITE BADGE!"))
    waitmessage()
    call(Common_EventScript_PlayGymBadgeFanfare)
    //setflag(FLAG_BADGE08_GET)
    setflag(FLAG_ROGUE_DEFEATED_BOSS11)
    msgbox(format("Good luck in your future battles!"))
    waitmessage()

    special(RemoveCameraObject)
    warp(MAP_ROGUE_HUB_TRANSITION, 0)
}

movement PlayerToBattleFieldBoss11 {
    walk_up * 10
}

movement CameraToBattleFieldBoss11 {
    walk_up * 3
}

movement PlayerToFightBoss11 {
    walk_up
}

movement BossToFightBoss11 {
    walk_down
}