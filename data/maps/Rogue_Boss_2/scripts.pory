const BOSS_NPC = 5

mapscripts Rogue_Boss_2_MapScripts
{
}

script Rogue_Battle_Boss2_Hint
{
    lock

    call(Rogue_GymIdentity_PreBoss)
    if(var(VAR_RESULT) == YES)
    {
        msgbox(format("A GYM LEADER of the type ELECTRIC awaits..."), MSGBOX_DEFAULT)
    }

    release
}

script Rogue_Battle_Boss2
{
    lockall()
    applymovement(OBJ_EVENT_ID_PLAYER, PlayerToBattleFieldBoss2)
    waitmovement(0)

    special(SpawnCameraObject)
    applymovement(OBJ_EVENT_ID_CAMERA, CameraToBattleFieldBoss2)
    waitmovement(0)

    msgbox(format("WATTSON: A new challenger!"))

    if(flag(FLAG_ROGUE_WEATHER_ACTIVE))
    {
        setweather(WEATHER_RAIN_THUNDERSTORM)
        doweather
    }

    applymovement(BOSS_NPC, BossToFightBoss2)
    applymovement(OBJ_EVENT_ID_PLAYER, PlayerToFightBoss2)
    waitmovement(0)
    
    // Manually kick off the encouter to control the music + avoid the trainer from changing directions
    playbgm(MUS_ENCOUNTER_INTENSE, FALSE)
    msgbox(format("Don't hold back now!"))
    trainerbattle(TRAINER_BATTLE_SINGLE_NO_INTRO_TEXT, TRAINER_WATTSON_1, 0, format("Well fought trainer!"))
    call(Rogue_BattleDefeated_Boss2)
}

script Rogue_BattleDefeated_Boss2
{
    setweather(WEATHER_NONE)
    doweather
    
    message(format("{PLAYER} received a GYM BADGE!"))
    waitmessage()
    call(Common_EventScript_PlayGymBadgeFanfare)
    setflag(FLAG_BADGE03_GET)
    setflag(FLAG_ROGUE_DEFEATED_BOSS02)
    msgbox(format("Good luck in your future battles!"))
    waitmessage()
    special(HealPlayerParty)
    special(RemoveCameraObject)
    warp(MAP_ROGUE_HUB_TRANSITION, 0)
}

movement PlayerToBattleFieldBoss2 {
    walk_up * 10
    walk_right
}

movement CameraToBattleFieldBoss2 {
    walk_right * 4
}

movement PlayerToFightBoss2 {
    walk_right
    face_right
}

movement BossToFightBoss2 {
    walk_left
}