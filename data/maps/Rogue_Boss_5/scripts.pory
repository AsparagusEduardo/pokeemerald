const BOSS_NPC = 5

mapscripts Rogue_Boss_5_MapScripts
{
}

script Rogue_Battle_Boss5
{
    lockall()
    applymovement(OBJ_EVENT_ID_PLAYER, PlayerToBattleFieldBoss5)
    waitmovement(0)

    special(SpawnCameraObject)
    applymovement(OBJ_EVENT_ID_CAMERA, CameraToBattleFieldBoss5)
    waitmovement(0)

    msgbox(format("WINONA: A new challenger!"))

    applymovement(BOSS_NPC, BossToFightBoss5)
    applymovement(OBJ_EVENT_ID_PLAYER, PlayerToFightBoss5)
    waitmovement(0)
    
    // Manually kick off the encouter to control the music + avoid the trainer from changing directions
    playbgm(MUS_ENCOUNTER_INTENSE, FALSE)
    msgbox(format("Don't hold back now!"))
    trainerbattle(TRAINER_BATTLE_SINGLE_NO_INTRO_TEXT, TRAINER_WINONA_1, 0, format("Well fought trainer!"))
    call(Rogue_BattleDefeated_Boss5)
}

script Rogue_BattleDefeated_Boss5
{
    message(format("{PLAYER} received a GYM BADGE!"))
    waitmessage()
    call(Common_EventScript_PlayGymBadgeFanfare)
    setflag(FLAG_BADGE06_GET)
    setflag(FLAG_ROGUE_DEFEATED_BOSS05)
    msgbox(format("Good luck in your future battles!"))
    waitmessage()

    special(RemoveCameraObject)
    warp(MAP_ROGUE_HUB_TRANSITION, 0)
}

movement PlayerToBattleFieldBoss5 {
    walk_up * 10
    walk_right
}

movement CameraToBattleFieldBoss5 {
    walk_right * 4
}

movement PlayerToFightBoss5 {
    walk_right
    face_right
}

movement BossToFightBoss5 {
    walk_left
}