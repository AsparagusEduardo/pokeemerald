const BOSS_NPC = 5

mapscripts Rogue_Boss_7_MapScripts
{
}

script Rogue_Battle_Boss7
{
    lockall()
    applymovement(OBJ_EVENT_ID_PLAYER, PlayerToBattleFieldBoss7)
    waitmovement(0)

    special(SpawnCameraObject)
    applymovement(OBJ_EVENT_ID_CAMERA, CameraToBattleFieldBoss7)
    waitmovement(0)

    msgbox(format("JUAN: A new challenger!"))

    if(flag(FLAG_ROGUE_WEATHER_ACTIVE))
    {
        setweather(WEATHER_RAIN)
        doweather
    }

    applymovement(BOSS_NPC, BossToFightBoss7)
    applymovement(OBJ_EVENT_ID_PLAYER, PlayerToFightBoss7)
    waitmovement(0)
    
    // Manually kick off the encouter to control the music + avoid the trainer from changing directions
    playbgm(MUS_ENCOUNTER_INTENSE, FALSE)
    msgbox(format("Don't hold back now!"))
    trainerbattle(TRAINER_BATTLE_SINGLE_NO_INTRO_TEXT, TRAINER_JUAN_1, 0, format("Well fought trainer!"))
    call(Rogue_BattleDefeated_Boss7)
}

script Rogue_BattleDefeated_Boss7
{
    message(format("{PLAYER} received a GYM BADGE!"))
    waitmessage()
    call(Common_EventScript_PlayGymBadgeFanfare)
    setflag(FLAG_BADGE08_GET)
    setflag(FLAG_ROGUE_DEFEATED_BOSS07)
    msgbox(format("Good luck in your future battles!"))
    waitmessage()
    special(HealPlayerParty)
    special(RemoveCameraObject)
    warp(MAP_ROGUE_HUB_TRANSITION, 0)
}

movement PlayerToBattleFieldBoss7 {
    walk_up * 10
    walk_right
}

movement CameraToBattleFieldBoss7 {
    walk_right * 4
}

movement PlayerToFightBoss7 {
    walk_right
    face_right
}

movement BossToFightBoss7 {
    walk_left
}