const NPC_BOX_LEFT = 3
const NPC_BOX_RIGHT = 1
const NPC_HOST = 2

const VAR_GAMESHOW_STAGE = VAR_TEMP_1

mapscripts Rogue_Encounter_GameShow_MapScripts 
{

}

script Rogue_Encounter_GameShow_TriggerIntro
{
    lockall

    msgbox(format("WAHEY!"), MSGBOX_DEFAULT)
    applymovement(NPC_HOST, GameShow_Host_Spin)
    waitmovement(0)

    msgbox(format("Welcome to my ever so lovingly setup GAME SHOW!\pThis game is simple!\nYou'll get 2 choices...\pOne might be good...\nOne might be bad..!\pNow!\nOn to Round 1!"), MSGBOX_DEFAULT)

    call(Rogue_Encounter_GameShow_DropBoxes)


    releaseall
    setvar(VAR_TEMP_0, 1)
    setvar(VAR_GAMESHOW_STAGE, 1)
}

script Rogue_Encounter_GameShow_RestoreBoxes
{
    waitse
    playse(SE_BALL_BOUNCE_1)

    hideobjectat(NPC_BOX_LEFT, MAP_ROGUE_ENCOUNTER_GAME_SHOW)
    hideobjectat(NPC_BOX_RIGHT, MAP_ROGUE_ENCOUNTER_GAME_SHOW)

    setobjectxy(NPC_BOX_LEFT, 5, 2)
    setobjectxy(NPC_BOX_RIGHT, 9, 2)

    showobjectat(NPC_BOX_LEFT, MAP_ROGUE_ENCOUNTER_GAME_SHOW)
    showobjectat(NPC_BOX_RIGHT, MAP_ROGUE_ENCOUNTER_GAME_SHOW)
}

script Rogue_Encounter_GameShow_DropBoxes
{
    waitse
    playse(SE_FALL)
    waitse

    applymovement(NPC_BOX_LEFT, GameShow_BoxFall)
    applymovement(NPC_BOX_RIGHT, GameShow_BoxFall)
    waitmovement(0)

    waitse
    playse(SE_BRIDGE_WALK)
}

script Rogue_Encounter_GameShow_NextRound
{
    if(var(VAR_GAMESHOW_STAGE) >= 3)
    {
        call(Rogue_Encounter_GameShow_Finish)
    }
    else
    {
        addvar(VAR_GAMESHOW_STAGE, 1)
        buffernumberstring(STR_VAR_1, VAR_GAMESHOW_STAGE)

        call(Rogue_Encounter_GameShow_RestoreBoxes)

        applymovement(NPC_HOST, GameShow_Host_Spin)
        waitmovement(0)

        msgbox(format("On to Round {STR_VAR_1}!"), MSGBOX_DEFAULT)
        call(Rogue_Encounter_GameShow_DropBoxes)
    }
}

script Rogue_Encounter_GameShow_Finish
{
    call(Rogue_Encounter_GameShow_RestoreBoxes)

    applymovement(NPC_HOST, GameShow_Host_Spin)
    waitmovement(0)

    msgbox(format("That's all for now folks!\nKe ke ke"), MSGBOX_DEFAULT)

    applymovement(NPC_HOST, GameShow_Host_Exit)
    waitmovement(0)

    setobjectxyperm(NPC_HOST, 7, 0)
}

script Rogue_Encounter_GameShow_ChooseInternal
{
    msgbox(format("TODO"))
    call(Rogue_Encounter_GameShow_NextRound)
}

script Rogue_Encounter_GameShow_ChooseLeft
{
    lock

    turnobject(NPC_HOST, DIR_WEST)

    msgbox(format("So you pick the LEFT?"), MSGBOX_YESNO)
    if(var(VAR_RESULT) == YES)
    {
        msgbox(format("The LEFT it is!"), MSGBOX_DEFAULT)
        call(Rogue_Encounter_GameShow_ChooseInternal)
    }

    release
}

script Rogue_Encounter_GameShow_ChooseRight
{
    lock

    turnobject(NPC_HOST, DIR_EAST)

    msgbox(format("So you pick the RIGHT?"), MSGBOX_YESNO)
    if(var(VAR_RESULT) == YES)
    {
        msgbox(format("The RIGHT it is!"), MSGBOX_DEFAULT)
        call(Rogue_Encounter_GameShow_ChooseInternal)
    }

    release
}

movement GameShow_BoxFall {
    walk_faster_down * 6
}

movement GameShow_Host_Spin
{
    face_right
    delay_1

    face_up
    delay_1

    face_left
    delay_1

    face_down
    delay_1
    
    face_right
    delay_1

    face_up
    delay_1

    face_left
    delay_1

    face_down
    delay_1

    jump_in_place_down
}

movement GameShow_Host_Exit {
    jump_2_up * 2
}