const BOSS_NPC = 5

mapscripts Rogue_Boss_1_MapScripts
{
}

script Rogue_Battle_Boss1_Hint
{
    lock

    call(Rogue_GymIdentity_PreBoss)
    if(var(VAR_RESULT) == YES)
    {
        msgbox(format("A GYM LEADER of the type FIGHTING awaits..."), MSGBOX_DEFAULT)
    }

    release
}

script Rogue_Battle_Boss1
{
    lockall()
    applymovement(OBJ_EVENT_ID_PLAYER, PlayerToBattleFieldBoss1)
    waitmovement(0)

    special(SpawnCameraObject)
    applymovement(OBJ_EVENT_ID_CAMERA, CameraToBattleFieldBoss1)
    waitmovement(0)

    msgbox(format("BRAWLY: A new challenger!"))

    applymovement(BOSS_NPC, BossToFightBoss1)
    applymovement(OBJ_EVENT_ID_PLAYER, PlayerToFightBoss1)
    waitmovement(0)
    
    // Manually kick off the encouter to control the music + avoid the trainer from changing directions
    playbgm(MUS_ENCOUNTER_INTENSE, FALSE)
    msgbox(format("Don't hold back now!"))
    trainerbattle(TRAINER_BATTLE_SINGLE_NO_INTRO_TEXT, TRAINER_BRAWLY_1, 0, format("Well fought trainer!"))
    call(Rogue_BattleDefeated_Boss1)
}

script Rogue_BattleDefeated_Boss1
{
    setweather(WEATHER_NONE)
    doweather
    
    message(format("{PLAYER} received a GYM BADGE!"))
    waitmessage()
    call(Common_EventScript_PlayGymBadgeFanfare)
    setflag(FLAG_BADGE02_GET)
    setflag(FLAG_ROGUE_DEFEATED_BOSS01)
    msgbox(format("Good luck in your future battles!"))
    waitmessage()
    special(HealPlayerParty)
    special(RemoveCameraObject)
    warp(MAP_ROGUE_HUB_TRANSITION, 0)
}

movement PlayerToBattleFieldBoss1 {
    walk_up * 10
    walk_right
}

movement CameraToBattleFieldBoss1 {
    walk_right * 4
}

movement PlayerToFightBoss1 {
    walk_right
    face_right
}

movement BossToFightBoss1 {
    walk_left
}