const FLAG_HIDE_BIRCH = FLAG_TEMP_1

const VAR_RETURN_STATE = VAR_TEMP_0

const OBJ_EVENT_BIRCH = 5

mapscripts Rogue_Area_TownSquare_MapScripts 
{
    MAP_SCRIPT_ON_LOAD: Rogue_Area_TownSquare_OnMapLoad
    MAP_SCRIPT_ON_FRAME_TABLE 
    [
        VAR_RETURN_STATE, 0: Rogue_Area_TownSquare_WarpState0
    ]
}

script Rogue_Area_TownSquare_OnMapLoad
{
    if(flag(FLAG_ROGUE_DEBUG_DISABLED))
    {
        fillmetatile(10, 17, 11, 17, METATILE_GeneralHub_Grass, FALSE)
    }

    if(var(VAR_ROGUE_INTRO_STATE) == ROGUE_INTRO_STATE_SPAWN)
    {
        clearflag(FLAG_HIDE_BIRCH)
    }
    else
    {
        setflag(FLAG_HIDE_BIRCH)
    }
}

script Rogue_Area_TownSquare_WarpState0
{
    if(var(VAR_ROGUE_INTRO_STATE) == ROGUE_INTRO_STATE_SPAWN)
    {
        call(Rogue_Area_TownSquare_StartGame)
    }
    setvar(VAR_RETURN_STATE, 1)
}

script Rogue_Area_TownSquare_StartGame
{
    lock

    applymovement(OBJ_EVENT_BIRCH, BirchStartGame0)
    applymovement(OBJ_EVENT_ID_PLAYER, PlayerStartGame0)
    waitmovement(0)

    msgbox(format("BIRCH: There you are!\nBack up on your feet now!\pYou seem fine, but perhaps it would be a good idea for you to have a wander around {POKEMON_HUB} to refresh your memory.\pOnce you're ready, come and find me in my lab."))
    closemessage

    applymovement(OBJ_EVENT_BIRCH, BirchStartGame1)
    waitmovement(0)

    opendoor(8, 8)
    waitdooranim

    applymovement(OBJ_EVENT_BIRCH, BirchStartGame2)
    waitmovement(0)
    hideobjectat(OBJ_EVENT_BIRCH, MAP_ROGUE_AREA_TOWN_SQUARE)

    closedoor(8, 8)
    waitdooranim

    addvar(VAR_ROGUE_INTRO_STATE, 1)
    release
}

movement BirchStartGame0 
{
    walk_in_place_right
}

movement PlayerStartGame0 
{
    face_left
}

movement BirchStartGame1
{
    walk_left
    walk_up * 3
}

movement BirchStartGame2
{
    walk_up
}

script Rogue_Area_TownSquare_LabSign
{
    msgbox(format("PROF. BIRCH's LAB"), MSGBOX_SIGN)
}

script Rogue_Area_TownSquare_ConfigSign
{
    msgbox(format("CONFIG LAB"), MSGBOX_SIGN)
}

script Rogue_Area_TownSquare_TalkBackpacker
{
    lock
    faceplayer

    msgbox(format("Hi there {PLAYER}!"))

    specialvar(VAR_0x8004, Rogue_GetBagCapacityUpgradeLevel)
    specialvar(VAR_0x8005, Rogue_GetBagAmountUpgradeLevel)

    if(var(VAR_0x8004) == ITEM_BAG_MAX_CAPACITY_UPGRADE && var(VAR_0x8005) == ITEM_BAG_MAX_AMOUNT_UPGRADE)
    {
        msgbox(format("I hope that bag's working out well for you!"))
    }
    else
    {
        specialvar(VAR_0x8006, Rogue_GetBagUpgradeCost)

        switch(var(VAR_0x8006))
        {
            case 0:
                bufferconstantnumberstring(STR_VAR_2, 1000)
            case 1:
                bufferconstantnumberstring(STR_VAR_2, 2000)
            case 2:
                bufferconstantnumberstring(STR_VAR_2, 3000)
            case 3:
                bufferconstantnumberstring(STR_VAR_2, 4000)
            case 4:
                bufferconstantnumberstring(STR_VAR_2, 5000)
            case 5:
                bufferconstantnumberstring(STR_VAR_2, 6000)
            case 6:
                bufferconstantnumberstring(STR_VAR_2, 7000)
            case 7:
                bufferconstantnumberstring(STR_VAR_2, 8000)
            case 8:
                bufferconstantnumberstring(STR_VAR_2, 9000)
            case 9:
                bufferconstantnumberstring(STR_VAR_2, 10000)
        }

        showmoneybox(0, 0)
        msgbox(format("I can upgrade your bag for Â¥{STR_VAR_2}, if you'd like?"), MSGBOX_YESNO)

        if(var(VAR_RESULT) == YES)
        {
            switch(var(VAR_0x8006))
            {
                case 0:
                    checkconstantmoney(1000)
                case 1:
                    checkconstantmoney(2000)
                case 2:
                    checkconstantmoney(3000)
                case 3:
                    checkconstantmoney(4000)
                case 4:
                    checkconstantmoney(5000)
                case 5:
                    checkconstantmoney(6000)
                case 6:
                    checkconstantmoney(7000)
                case 7:
                    checkconstantmoney(8000)
                case 8:
                    checkconstantmoney(9000)
                case 9:
                    checkconstantmoney(10000)
            }

            if(var(VAR_RESULT) == YES)
            {
                switch(var(VAR_0x8006))
                {
                    case 0:
                        removeconstantmoney(1000)
                    case 1:
                        removeconstantmoney(2000)
                    case 2:
                        removeconstantmoney(3000)
                    case 3:
                        removeconstantmoney(4000)
                    case 4:
                        removeconstantmoney(5000)
                    case 5:
                        removeconstantmoney(6000)
                    case 6:
                        removeconstantmoney(7000)
                    case 7:
                        removeconstantmoney(8000)
                    case 8:
                        removeconstantmoney(9000)
                    case 9:
                        removeconstantmoney(10000)
                }
                updatemoneybox

                playse(SE_SHOP)
                waitse
                
                // Upgrade amount
                if(var(VAR_0x8005) < VAR_0x8004)
                {
                    special(Rogue_IncreaseBagAmountUpgradeLevel)
                    message(format("{COLOR GREEN}{SHADOW LIGHT_GREEN}Upgraded bag stack size!"))
                    waitmessage
                    playfanfare(MUS_LEVEL_UP)
                    waitfanfare
                }
                // Upgrade capacity (Always upgrade this first)
                else
                {
                    special(Rogue_IncreaseBagCapacityUpgradeLevel)
                    message(format("{COLOR GREEN}{SHADOW LIGHT_GREEN}Upgraded bag carry capacity!"))
                    waitmessage
                    playfanfare(MUS_LEVEL_UP)
                    waitfanfare
                }
            }
            else
            {
                msgbox(format("It doesn't look like you have enough money there bud."))
            }
        }
        else
        {
            msgbox(format("Sure! You're busy!\pI get that!"))
        }

        hidemoneybox
    }

    release
}